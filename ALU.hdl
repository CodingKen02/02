// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
    Mux16(a=x, b=false, sel=zx, out=muxout1);
    Mux16(a=y, b=false, sel=zy, out=muxout2);
    Not16(in=muxout1, out=notout1);
    Mux16(a=muxout1, b=notout1, sel=nx, out=muxout3);
    Not16(in=muxout2, out=notout2);
    Mux16(a=muxout2, b=notout2, sel=ny, out=muxout4);
    And16(a=muxout3, b=muxout4, out=andout1);
    Add16(a=muxout3, b=muxout4, out=addout1);
    Mux16(a=andout1, b=addout1, sel=f, out=muxout5);
    Not16(in=muxout5, out=notout3);
    Mux16(a=muxout5, b=notout3, sel=no, out=out, out=zr0, out[0..7]=zr1, out[8..15]=zr2, out[15]=ng);
    Or8Way(in=zr1, out=or8out1);
    Or8Way(in=zr2, out=or8out2);
    Or(a=or8out1, b=or8out2, out=orout);
    Not(in=orout, out=zr);

    // each input of the ALU chip has a meaning
    // depending on the 16-bit values inserted, the chip will display the values processed by each function listed above
    // the output will either tell you if the final value is less than or equal to 0 (zr: out == 0 and ng: out < 0)
}

